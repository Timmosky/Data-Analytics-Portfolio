-----Comparing against prior (selected) periods------
TrailingUpperLimit = 
VAR SelectedMeanWeeks = SELECTEDVALUE( ALERTWINDOWSELECTOR[TrailingWindow], 8 )
VAR SelectedStdWeeks  = SELECTEDVALUE( 'STDDEVIATION SELECTOR'[TrailingWindow], SelectedMeanWeeks )
VAR Multiplier = SELECTEDVALUE( StdDevSelector[Multiplier], 2 )

/* Current week's date - find the max date in current row context */
VAR CurrentWeek = SELECTEDVALUE(parts[Order Fiscal Week])
VAR CurrentMaxDate = 
    CALCULATE(
        MAX(parts[Order Date]),
        parts[Order Fiscal Week] = CurrentWeek,
        ALLEXCEPT(parts, parts[Item Description], parts[Ultimate_family_name])
    )

/* Define window BEFORE current week */
VAR MeanStartDate = CurrentMaxDate - ( SelectedMeanWeeks * 7 )
VAR MeanEndDate = CurrentMaxDate - 1  /* Exclude current week */
VAR StdStartDate = CurrentMaxDate - ( SelectedStdWeeks * 7 )
VAR StdEndDate = CurrentMaxDate - 1

VAR CurrentItem = SELECTEDVALUE(parts[Item Description])
VAR CurrentFamily = SELECTEDVALUE(parts[Ultimate_family_name])

/* Summary for Mean - excludes current week */
VAR SummaryForMean =
    ADDCOLUMNS(
        CALCULATETABLE(
            SUMMARIZE(
                parts,
                parts[Order Fiscal Week],
                parts[Item Description],
                parts[Ultimate_family_name]
            ),
            parts[Item Description] = CurrentItem,
            parts[Ultimate_family_name] = CurrentFamily,
            parts[Order Date] >= MeanStartDate,
            parts[Order Date] <= MeanEndDate,
            parts[Order Fiscal Week] <> CurrentWeek  /* Extra safety */
        ),
        "NR_Value", [NR_Measure]
    )

/* Summary for StdDev - excludes current week */
VAR SummaryForStd =
    ADDCOLUMNS(
        CALCULATETABLE(
            SUMMARIZE(
                parts,
                parts[Order Fiscal Week],
                parts[Item Description],
                parts[Ultimate_family_name]
            ),
            parts[Item Description] = CurrentItem,
            parts[Ultimate_family_name] = CurrentFamily,
            parts[Order Date] >= StdStartDate,
            parts[Order Date] <= StdEndDate,
            parts[Order Fiscal Week] <> CurrentWeek  /* Extra safety */
        ),
        "NR_Value", [NR_Measure]
    )

VAR FilteredMean = FILTER(SummaryForMean, NOT ISBLANK([NR_Value]))
VAR FilteredStd = FILTER(SummaryForStd, NOT ISBLANK([NR_Value]))

VAR Mean = AVERAGEX(FilteredMean, [NR_Value])
VAR StdDevv = STDEVX.P(FilteredStd, [NR_Value])

RETURN
IF(
    NOT ISBLANK(Mean) && NOT ISBLANK(StdDevv),
    Mean + ( Multiplier * StdDevv ),
    BLANK()
)



------------Comparing new period based on historical (selected period) data---------------
TrailingUpperLimitCOPY = 
VAR SelectedMeanWeeks = SELECTEDVALUE( ALERTWINDOWSELECTOR[TrailingWindow], 104 ) /* Default mean window i set is 2 years */
VAR SelectedStdWeeks  = SELECTEDVALUE( 'STDDEVIATION SELECTOR'[TrailingWindow], SelectedMeanWeeks )  // I am keeping this as a default fallback

VAR Multiplier = SELECTEDVALUE( StdDevSelector[Multiplier], 2 )

VAR MaxDate = CALCULATE(MAX(parts[Order Date]))

/* mean window start (mean selector) */
VAR MeanStartDate = MaxDate - ( SelectedMeanWeeks * 7 ) + 1

/* std window start (std selector) */
VAR StdStartDate = MaxDate - ( SelectedStdWeeks * 7 ) + 1

/* filtered table for mean (keeps the ALLEXCEPT logic for Item Description) */
VAR FilteredForMean =
    FILTER(
        ALLEXCEPT( Parts, Parts[Item Description], parts[Ultimate_family_name]), 
        Parts[Order Date] >= MeanStartDate
            && Parts[Order Date] <= MaxDate
            && NOT ISBLANK(parts[Normal_rate])
    )

/* filtered table for std (same scope but different date window) */
VAR FilteredForStd =
    FILTER(
        ALLEXCEPT( Parts, Parts[Item Description], parts[Ultimate_family_name]),
        Parts[Order Date] >= StdStartDate
            && Parts[Order Date] <= MaxDate
            && NOT ISBLANK(parts[Normal_rate])
    )

VAR Mean = CALCULATE(AVERAGEX(FilteredForMean, parts[Normal_rate]))
VAR StdDevv = CALCULATE(STDEVX.P(FilteredForStd, parts[Normal_rate]))

RETURN
Mean + ( Multiplier * StdDevv )



VAR Exceedance8Week = [Exceedance_pp]
VAR ExceedanceQuarterly = [Exceedance_pp_YOY_]
VAR CurrentQuarter = [Current_Quarter_Number]
VAR TriggerQuarter = [First_8Week_Trigger_Quarter_Rolling]
VAR QD = [QD]
VAR Validquarter = [Is_Sustained_Anomaly_Valid]

RETURN
SWITCH(
    TRUE(),
    
    /* PRIORITY 2: Quarterly ≥2 AND within valid range (0-2 quarters) → Sustained */
    ExceedanceQuarterly >= 2 && Validquarter = 1 ,
        "Sustained Anomaly Detected",
    
    /* PRIORITY 3: 8-week spike outside trigger quarter */
    Exceedance8Week >= 2, 
        "Anomaly Detected",
    
    /* PRIORITY 4: Quarterly ≥2 but OUTSIDE valid range (3+ quarters or no trigger) */
    ExceedanceQuarterly >= 2, 
        "Quarterly Anomaly (Out of Range)",
    
    /* PRIORITY 5: Individual spikes */
    [TrailingAnomaly] = 1 || [Quarterly_YoY_Anomaly] = 1, 
        "Single Spike",
    
    /* PRIORITY 6: Normal */
    "Normal"
)

---------------------------------
Exceedance_pp = 
var currentweek = MAX(parts[Order Fiscal Week])
VAR currentitem = MAX(parts[Item Description]) 
VAR current_family = MAX(parts[Ultimate_family_name])
Var threshold = SELECTEDVALUE('Exceedance Period'[Exceedance Period],1)
var Maxlookback = 52
Var offsets = GENERATESERIES(0, Maxlookback, 1)

var firstzerooffset = MINX(offsets, Var o = [Value] 

VAR wkDate = currentweek - o 
VAR f1 = MAXX(FILTER(ALL(parts[Item Description], parts[Ultimate_family_name], parts[Order Fiscal Week]), parts[Item Description] = currentitem && parts[Ultimate_family_name] = current_family && parts[Order Fiscal Week] = wkDate),parts[Exceedance_Flag])


RETURN IF(COALESCE( f1, 0) = 0, o, BLANK()))

var streak = IF(ISBLANK(firstzerooffset), Maxlookback + 1 , firstzerooffset)

RETURN
IF(streak >= threshold, streak, BLANK())



---------------------------------------------------------------------------------
Exceedance_pp_Column = 
VAR CurrentWeek = parts[Order Fiscal Week]
VAR CurrentItem = parts[Item Description]
VAR CurrentFamily = parts[Ultimate_family_name]
VAR IsAnomaly = parts[Exceedance_Flag] = 1

RETURN
IF(
    IsAnomaly,
    
    /* Find the last non-anomaly week before current week */
    VAR LastNonAnomalyWeek = 
        MAXX(
            FILTER(
                parts,
                parts[Item Description] = CurrentItem
                && parts[Ultimate_family_name] = CurrentFamily
                && parts[Order Fiscal Week] < CurrentWeek
                && parts[Exceedance_Flag] = 0
            ),
            parts[Order Fiscal Week]
        )
    
    /* Set streak start (0 if no gap found) */
    VAR StreakStart = IF( ISBLANK(LastNonAnomalyWeek), 0, LastNonAnomalyWeek )
    
    /* Count anomaly weeks from streak start to current week */
    VAR StreakLength = 
        COUNTROWS(
            FILTER(
                parts,
                parts[Item Description] = CurrentItem
                && parts[Ultimate_family_name] = CurrentFamily
                && parts[Order Fiscal Week] > StreakStart
                && parts[Order Fiscal Week] <= CurrentWeek
                && parts[Exceedance_Flag] = 1
            )
        )
    
    RETURN StreakLength,
    
    BLANK()
)


---------------------------------------

combinedanomaly = 
VAR Exceedance8Week = MAX( parts[Exceedance_pp_Column] )  /* Column, not measure! */
VAR ExceedanceQuarterly = MAX( parts[Exceedance_pp_YOY_Column] )  /* Column, not measure! */
VAR CurrentQuarter = [Current_Quarter_Number]
VAR TriggerQuarter = [First_8Week_Trigger_Quarter_Rolling]
VAR Validquarter = [Is_Sustained_Anomaly_Valid]

RETURN
SWITCH(
    TRUE(),
    ExceedanceQuarterly >= 2 && Validquarter = 1, "Sustained Anomaly Detected",
    Exceedance8Week >= 2, "Anomaly Detected",
    ExceedanceQuarterly >= 2, "Quarterly Anomaly (Out of Range)",
    MAX( parts[TrailingAnomaly] ) = 1 || MAX( parts[Quarterly_YoY_Anomaly] ) = 1, "Single Spike",
    "Normal"
)
